#include "imports/stdlib.fc";
#include "imports/utils.fc";

int op::commit() asm "1 PUSHINT";
int workchain() asm "0 PUSHINT";

(slice, cell) load_data() inline {
    var ds = get_data().begin_parse();
    return (
        ds~load_msg_addr(),
        ds~load_ref()
    );
}

(slice) get_address() method_id {
    var (owner_address, commitment_code) = load_data();
    return (
        owner_address
    );
}

int balance() method_id {
    var [balance, _] = get_balance();
    return balance;
}

cell pack_commitment_data(cell commitment_description) inline {
    int commitment_description_length = get_cell_tree_size(commitment_description);
    ;;maximum text size - 1000 chars in UTF-32 encoding
    ;;todo - add exit code description
    throw_if(75, commitment_description_length > 32000);
    return  begin_cell()
        .store_ref(commitment_description)
        .end_cell();
}

cell calculate_commitment_state_init(cell commitment_code, cell commitment_description) inline {
    return begin_cell()
        .store_uint(0, 1) ;; split_depth:(Maybe (## 5))
        .store_uint(0, 1) ;; special:(Maybe TickTock)
        .store_uint(1, 1) ;; (Maybe ^Cell)
        .store_uint(1, 1) ;; (Maybe ^Cell)
        .store_uint(0, 1) ;; (HashmapE 256 SimpleLib)
        .store_ref(commitment_code)
        .store_ref(pack_commitment_data(commitment_description))
        .end_cell();
}

slice calculate_commitment_address(cell state_init) inline {
    return begin_cell().store_uint(4, 3)
        .store_int(workchain(), 8)
        .store_uint(cell_hash(state_init), 256)
        .end_cell()
        .begin_parse();
}

() make_commitment( cell commitment_code, int coins_for_deploy, cell commitment_description) impure {
    cell state_init = calculate_commitment_state_init(commitment_code, commitment_description);
    slice to_commitment_address = calculate_commitment_address(state_init);

    var msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(to_commitment_address)
        .store_coins(coins_for_deploy)
        .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
        .store_ref(state_init)
        ;;do nothing on recv_internal after deployment
        .store_ref(begin_cell().end_cell())
     .end_cell();
    send_raw_message(msg, 1);
}


() recv_internal(int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) {
        ;; ignore empty messages
        return ();
    }
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    if (flags & 1) {
        ;; ignore all bounced messages
        return ();
    }

    slice sender_address = cs~load_msg_addr();
    int op = in_msg_body~load_uint(32);

    (slice owner_address, cell commitment_code) = load_data();

    if (op == op::commit()) {
        ;;todo create code errors table
        throw_unless(73, equal_slice_bits(sender_address, owner_address));
        int coins_for_deploy = in_msg_body~load_coins();
        cell commitment_description = in_msg_body~load_ref();

        make_commitment(commitment_code, coins_for_deploy, commitment_description);

        ;;todo create code errors table
        return ();
    }
}
